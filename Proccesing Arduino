//Jefferson Malte
//Juego Ping-Pong

import processing.serial.*; // biblioteca para comunicación serial
import ddf.minim.*; // biblioteca para generar sonidos
import controlP5.*;

ControlP5 cp5;

String portname = "COM10";
Serial Port; // objeto Serial
// variables
int[] posicion;
int pad1 = 100;
int pad2 = 15;
int dim_pantalla = 20;
float p1pos,p2pos;
float x,y,v_x,v_y;
float init_x,init_y;
int rebotes;
boolean start = false;
boolean iniciar = true;
int punto1,punto2;
boolean ganar = false;
boolean titulo = true;
boolean pausa = false;

// Iniciar biblioteca y se define sonidos a usar en el juego
Minim minim;
AudioPlayer sonido_ping;
AudioPlayer sonido_pong;

void setup(){
  size(900,600); //  resolución de monitor
  init_x = width/2;
  init_y = height/2;
  noCursor();
  Port = new Serial(this, portname, 9600);
  Port.bufferUntil('\n');
  textAlign(CENTER);
  minim = new Minim(this);
  sonido_ping = minim.loadFile("ping.mp3");
  sonido_pong = minim.loadFile("pong.mp3");
  
  cp5=new ControlP5(this);
  //boton
  cp5.addButton("ABRIR")
    .setPosition(width/2-60,0)
    .setSize(55,15)
    .setColorBackground(#0052FF) //color azul     #F51111
    .setColorActive(#F51111)//color rojo     #E39595
    .setColorForeground(#00FF0A)//color verde
    ;
    cp5.addButton("PAUSE")
    .setPosition(width/2+5,0)
    .setSize(55,15)
    .setColorBackground(#0052FF) //color azul     #F51111
    .setColorActive(#F51111)//color rojo     #E39595
    .setColorForeground(#00FF0A)//color verde
    ;
}

void draw(){
 if(!pausa){ 
  if(ganar){
    puntaje();
    if(punto1>punto2){
      stroke(0);
      strokeWeight(2);
      fill(0);
      textAlign(CENTER,CENTER);
      textSize(90);
      text("Ganó",width*0.25,height/2);
      text("Perdió",width*0.75,height/2);
      textSize(70);
      strokeWeight(2);
    }
    else{
      stroke(0);
      strokeWeight(2);
      fill(0);
      textAlign(CENTER,CENTER);
      textSize(90);
      text("Perdió",width*0.25,height/2);
      text("Ganó",width*0.75,height/2);
      textSize(70);
      strokeWeight(2);
    }
  }
  
  if(titulo){
    background(#CDEEF5);//color PP  #FFCC00
    pantalla_titulo();
    fill(255);
  }
  
  if(start){
    textSize(100);
    background(#444444);
    campo();
    fill(255);
    puntaje();
    if(iniciar){
      x = init_x;
      y = init_y;
      v_x = random(2,3)*rnd_sign();
      v_y = random(2,3)*rnd_sign();
      rebotes = 0;
      iniciar = !iniciar;
    }
    posicionPad();
    pelota();
    }
 }
}

void serialEvent (Serial Port) {
  String stringa = Port.readStringUntil('\n');
  if (stringa != null) {
    stringa = trim(stringa);
    posicion = get_position(stringa);
  }

  
}

float rnd_sign(){
  float n = random(-1,1);
  if(n>=0){
    return 1.0;
  }
  else{
    return -1.0;
  }
}

void campo(){
  stroke(0);
  strokeWeight(2);
  fill(0,255,0);
  rectMode(CORNER);
  rect(0,0,width,10);
  rect(0,height-10,width,10);
  strokeWeight(2);
  stroke(#FC7303);
  int segmenti=25;
  for(int i=0;i<segmenti;i++){
    if(i%2==0){
      line(width/2,i*(height/segmenti),width/2,i*(height/segmenti)+(height/segmenti));
    }
  }
  noFill();
  ellipse(width/2,height/2,150,150);
  fill(255);
  ellipse(width/2,height/2,20,20);
}

void pantalla_titulo(){
  stroke(0);
  strokeWeight(2);
  fill(0);
  rectMode(CORNER);
  rect(0,0,width,10);
  rect(0,height-10,width,10);
  strokeWeight(2);
  PFont font;
  font =loadFont("Algerian-48.vlw");
  textFont(font);
  textAlign(CENTER,CENTER);
  textSize(60);
  fill(255,0,0);
  text("BIENVENIDOS\n\nLISTOS PARA JUGAR!!\n\nPING PONG",width/2,height*0.35);
  PFont font1;
  font1 =loadFont("ComicSansMS-BoldItalic-48.vlw");
  textFont(font1);
  textSize(40);
  fill(0,0,255);
  text("JUGAR ->Enter         PAUSE ->Espacio",width/2,height*0.8);
}

int[] get_position(String movimientos){
  return int(split(movimientos,'-'));
}

void posicionPad(){
  p1pos = map(posicion[0],0,1023,10+pad1/2,height-(10+pad1/2));
  p2pos = map(posicion[1],0,1023,10+pad1/2,height-(10+pad1/2));
  stroke(0);
  fill(0,0,255);
  rectMode(CENTER);
  rect(20+pad2/2,p1pos,pad2,pad1,5);
  fill(255,0,0);
  rect(width-(20+pad2/2),p2pos,pad2,pad1,5);
}

void pelota(){
  fill(#F8FC03);
  stroke(0);
  ellipse(x,y,dim_pantalla,dim_pantalla);
  x=x+v_x;
  y=y+v_y;
  
  if(y<10+dim_pantalla/2 || y>height-(10+dim_pantalla/2)){
    v_y=-v_y;
    sonido_ping.pause();
    sonido_ping.rewind();
    sonido_pong.pause();
    sonido_pong.rewind();
    sonido_pong.play();
  }
  if((v_x<0 && x<20+pad2+dim_pantalla/2 && x>20+pad2/2 && y>=p1pos-pad1/2 && y<=p1pos+pad1/2) || (v_x>0 && x>width-(20+pad2+dim_pantalla/2) && x<width-(20+pad2/2) && y>=p2pos-pad1/2 && y<=p2pos+pad1/2)){
    if(rebotes<20){
      v_x = -1.1*v_x;
      rebotes++;
    }
    else{
      v_x = -v_x;
    }
    sonido_pong.pause();
    sonido_pong.rewind();
    sonido_ping.pause();
    sonido_ping.rewind();
    sonido_ping.play();
  }
  if(x<0){
    punto2++;
    iniciar = true;
  }
  if(x>width){
    punto1++;
    iniciar = true;
  }
}

void puntaje(){
  textSize(90);
  text(punto1,(width/2)*0.85,height/6);
  text(punto2,(width/2)*1.15,height/6);
  textSize(70);
  if(punto1 == 5){
    ganar = true;
    start = false;
  }
  if(punto2 == 5){
    ganar = true;
    start = false;
  }
}

void keyPressed(){
  if((keyCode == 10 && !pausa)||(keyCode == 10 && !pausa)){
    start = !start;
    iniciar = true;
    punto1 = 0;
    punto2 = 0;
    titulo = !titulo;  
  }
  if(ganar){
    ganar = false;
    start = false;
    titulo = true;
  }
  if((keyCode == 32 && start)||(keyCode == 32 && !pausa)){
    pausa = !pausa;
  }
}

public void ABRIR(){
   start = !start;
    iniciar = true;
    punto1 = 0;
    punto2 = 0;
    titulo = !titulo; 
}

public void PAUSE(){
  pausa = !pausa;
}
